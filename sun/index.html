<!doctype html>
<html>

<head>
  <title>Procedual skydome</title>

  <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
  <link rel="stylesheet" type="text/css" href="css/style.css">

</head>

<body>

  <!-- Libs -->
  <script src="js/three.min.js"></script>
  <script src="js/OrbitControls.js"></script>

  <!-- My files -->
  <script src="js/vis.js"></script>


  <script type="x-shader/x-vertex" id="vsSkydome">
			varying vec3 vWorldPosition;
			varying vec2 vUV;

			void main() {

				vec4 worldPosition = modelMatrix * vec4( position, 1.0 );
				vWorldPosition = position.xyz;
				vUV = uv;

				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
			}
		</script>

  <script type="x-shader/x-fragment" id="fsSkydome">

//
// GLSL textureless classic 3D noise "cnoise",
// with an RSL-style periodic variant "pnoise".
// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
// Version: 2011-10-11
//
// Many thanks to Ian McEwan of Ashima Arts for the
// ideas for permutation and gradient selection.
//
// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
// Distributed under the MIT license. See LICENSE file.
// https://github.com/ashima/webgl-noise
//

vec3 mod289(vec3 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x)
{
  return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

vec3 fade(vec3 t) {
  return t*t*t*(t*(t*6.0-15.0)+10.0);
}

// Classic Perlin noise
float cnoise(vec3 P)
{
  vec3 Pi0 = floor(P); // Integer part for indexing
  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;

  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);

  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);

  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}

// Classic Perlin noise, periodic variant
float pnoise(vec3 P, vec3 rep)
{
  vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
  vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;

  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);

  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);

  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}

			uniform int time;
			uniform float cloudMove;
			uniform float width;
			uniform float height;

			varying vec3 vWorldPosition;
			varying vec2 vUV;

			const float toDeg = 57.2957795;

			float calcAngle(vec3 v1, vec3 direction)
			{
				float angle = acos( v1.x * direction.x + v1.y * direction.y + v1.z * direction.z);


				if (angle < 0.0) {
					angle = (180.0 - angle  * toDeg) / 180.0;
				}
				else {
					angle = (angle * toDeg) / 180.0;
				}

				return angle;
			}

			vec4 getClouds()
			{
				const int octaves = 8;
				
				float f = 22.0; 
				float amp = 3.0;
				float totalAmp = 0.0;
				float noiseValue = 0.0;
				float per =  1.0/12000.0;

				const vec3 period = vec3(1.0/9000.0, 1.0/3000.0, 1.0/9000.0) * 0.5;
				vec3 freq = f * vec3( 0.2, 0.5, 0.5 );
				vec3 cloudColor = vec3(1.0, 0.7, 0.7);

				vec3 seed = vec3(vWorldPosition.x * period.x, 
								 vWorldPosition.y * period.y, 
							  	 vWorldPosition.z * period.z);

				float speed = 10.0;
				seed += cloudMove * speed * vec3(1.0, 0.2, 0.2);

				for(int i = 0; i < octaves; ++i)
				{
					noiseValue += amp * cnoise( vec3(seed.x * freq.x, seed.y * freq.y, seed.z * freq.z) );
					totalAmp += amp;

					freq = freq * 2.0;
					amp  = amp / 2.0;
				}

				noiseValue = ((noiseValue + totalAmp) * 0.5) / totalAmp;

				return vec4(cloudColor, noiseValue);

			}

			vec3 background()
			{
				vec3 top = vec3(1.0, 1.0, 1.0) * 0.5;
				vec3 bottom = vec3(0.0, 0.0, 0.6);

				float smoothValue = smoothstep(0.0, 1.0, vUV.y);
				vec3 bg = smoothValue * top +
						  (1.0 - smoothValue) * bottom;

				return bg;
			}

			vec4 sun()
			{
				vec2 sunPosition = vec2(0.2, 0.5);
				vec3 sunColor = vec3(1.0, 0.6, 0.0) * 1.0;
				vec3 fluctuationColor = vec3(1.0, 0.0, 0.0);

				float sunSize = 0.07;
				float fluctuationAmount = 0.8;
				float fluctuation  = (fluctuationAmount * sunSize) * 
									 cos( float(time)  * (6.28 * 0.00005));

				fluctuation = abs(fluctuation);

				float distToSun = length( vUV - sunPosition);

				float innerSunAlpha = (1.0 - step(sunSize, distToSun)) * 
									  smoothstep(sunSize, sunSize * 0.80, distToSun);

				float outerSunAlpha = step(sunSize * 0.80, distToSun) * 
									  smoothstep(sunSize + fluctuation, sunSize * 0.80, distToSun);

				vec3 sun =  innerSunAlpha * sunColor +
							outerSunAlpha * fluctuationColor;

				return vec4(sun, min(innerSunAlpha + outerSunAlpha, 1.0) ) ;
			}

			void main() 
			{
			    /* Screenspace  */
			    vec2 p = gl_FragCoord.xy;
			    p.x = p.x / width;
			    p.y = p.y / height; 

				/* 1.0 Background */
				vec3 bgColor = background();

				/* 2.0 Clouds */
				vec4 cloudColor4 = getClouds();
				vec3 cloudColor = cloudColor4.xyz * cloudColor4.w;

				/* 3.0 Sun */
				vec4 sunColor4 = sun();
				vec3 sunColor = sunColor4.xyz;

				/* Put all together (combine) */
				vec3 finalColor = cloudColor;
				finalColor += (1.0 - cloudColor4.w) * sunColor;
				finalColor += (1.0 - cloudColor4.w) * (1.0 - sunColor4.w) * bgColor;

			    /* apply gamma correction */
			    float gamma = 2.2;
			    finalColor = pow(finalColor, vec3(1.0/gamma));

				//float avg = 0.2126 * finalColor.x + 0.7152 * finalColor.y + 0.0722 * finalColor.z;
				//gl_FragColor = vec4(avg, avg, avg, 1.0);
				

				gl_FragColor = vec4(finalColor, 1.0);

				//gl_FragColor = vec4(floor(finalColor * 8.0) * (1.0/8.0), 1.0);
			}
		</script>

  <div id="VisContainer"></div>

</body>

</html>